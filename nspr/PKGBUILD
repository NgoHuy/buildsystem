# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Original Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgdesc="Netscape Portable Runtime"
arch=(i686 x86_64)
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
depends=('glibc')
makedepends=('zip')
options=('!emptydirs')
source=(ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${pkgbase}-${pkgver}.tar.gz)

build() {
  cd $pkgbase-$pkgver

  if [[ $CARCH = x86_64 ]]; then
    _confflags="--enable-64bit"
  else
    _confflags=""
  fi

  ./nspr/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug ${_confflags}
  make
}

package() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  chmod 644 "$pkgdir"/usr/lib/*.a
  ln -s nspr.pc "$pkgdir/usr/lib/pkgconfig/mozilla-nspr.pc"
  rm -r "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties} \
         "$pkgdir/usr/include/nspr/md"
}
