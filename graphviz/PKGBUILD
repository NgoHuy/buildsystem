# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Original Contributor: kevin <kevin@archlinux.org>
# Original Contributor: John Proctor <jproctor@prium.net>

pkgdesc='Graph visualization software'
url='http://www.graphviz.org/'
license=('CPL')
arch=('i686' 'x86_64')
depends=('libltdl' 'gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'gts')
makedepends=('swig' 'mono' 'guile' 'lua51' 'ocaml' 'perl' 'php' 'python2' 'r' 'ruby' 'tk' 'qt4')
optdepends=('mono: sharp bindings'
            'guile: guile bindings'
            'lua51: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'php: php bindings'
            'python2: python bindings'
            'r: r bindings'
            'ruby: ruby bindings'
            'tcl: tcl bindings'
            'qt4: gvedit')
source=("${url}/pub/${pkgbase}/stable/SOURCES/${pkgbase}-${pkgver}.tar.gz")

options=('!libtool')
install=install

prepare() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	sed \
		-e 's: qw(\([^)]*\)): (qw(\1)):g' \
		-i config/config_perl.pl
	sed \
		-e '/"archdir"/s:$:+" '"$(echo -I/usr/include/ruby-2.0.0/{$CARCH-linux,ruby/backward,})"'":' \
		-e '/CONFIG/s:site:vendor:' \
		-i config/config_ruby.rb
	sed \
		-e 's:guile-1.8:guile-2.0:g' \
		-e 's:ruby-1.9:ruby-2.0:g' \
		-e '/LIBPOSTFIX="64"/d' \
		-i configure
}

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	export PYTHON=python2
	export LUA=lua5.1

	./configure --prefix=/usr --with-cgraph=no
	make || sed '/caml_array_length/d' -i tclpkg/gv/gv_ocaml.cpp # swig goes wonky
	make
}

package() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
