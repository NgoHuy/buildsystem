# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: SÃ©bastien Luttringer
# Original Contributor: Balwinder S "bsd" Dheeman (bdheeman AT gmail.com)

pkgdesc='Dynamic Kernel Modules System'
arch=('any')
url='http://linux.dell.com/dkms/'
license=('GPL2')
depends=('bash' 'kmod' 'gcc' 'make' 'patch')
optdepends=('linux-headers: build modules against Arch kernel'
            'linux-lts-headers: build modules against LTS Arch kernel')
backup=('etc/dkms/framework.conf' 'etc/default/dkms')
install=$pkgbase.install
source=("http://linux.dell.com/$pkgbase/permalink/$pkgbase-$pkgver.tar.gz"
        "$pkgbase.default"
        "$pkgbase.service"
        "$pkgbase.systemd"
        '01-broken-uninstall.patch'
        '02-no-kernel-hook.patch')

prepare() {
  # patch
  patches=("$srcdir"/*.patch)
  cd $pkgbase-$pkgver
  for p in "${patches[@]}"; do
    msg2 "Apply patch: ${p##*/}"
    patch -p1 -i "$p"
  done
  # /usr move
  msg2 '/usr move patching'
  for i in dkms{,_framework.conf,.bash-completion,.8,_common.postinst}; do
    sed -ri 's,/lib/modules,/usr/lib/modules,g' "$i"
  done
  # fix hardcoded paths
  sed -i "s|/sbin/depmod|depmod|" dkms
}

package() {
  # systemd
  install -D -m 644 $pkgbase.service "$pkgdir/usr/lib/systemd/system/$pkgbase.service"
  install -D -m 755 $pkgbase.systemd "$pkgdir/usr/lib/systemd/scripts/$pkgbase"
  install -D -m 644 $pkgbase.default "$pkgdir/etc/default/$pkgbase"
  # upstream installer
  cd $pkgbase-$pkgver
  make \
    DESTDIR="$pkgdir" \
    SBIN="$pkgdir/usr/bin" \
    BASHDIR="$pkgdir/usr/share/bash-completion/completions" \
    install
}

# vim:set ts=2 sw=2 et:
