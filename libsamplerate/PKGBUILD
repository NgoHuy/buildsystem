# $Id: PKGBUILD 192311 2013-08-10 02:14:39Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libsamplerate
pkgver=0.1.8
pkgrel=2
pkgdesc="Secret Rabbit Code - aka Sample Rate Converter for audio"
arch=('i686' 'x86_64')
url="http://www.mega-nerd.com/SRC/index.html"
license=('GPL')
depends=('libsndfile')
options=('!libtool')
source=(http://www.mega-nerd.com/SRC/libsamplerate-${pkgver}.tar.gz{,.asc})
sha1sums=('e5fe82c4786be2fa33ca6bd4897db4868347fe70'
          'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
