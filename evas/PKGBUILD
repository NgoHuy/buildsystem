# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Ronald van Haren <ronald@archlinux.org>
# Original Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgdesc="A hardware-accelerated canvas API for X-Windows"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('eet' 'giflib' 'libpng' 'libjpeg' 'libxrender' 'mesa'
  'fribidi' 'fontconfig' 'freetype2' 'harfbuzz' 'evas_generic_loaders')
options=('!libtool' '!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgbase-$pkgver.tar.gz)

build() {
  cd $pkgbase-$pkgver

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/evas \
    --enable-strict \
    --disable-static \
    --enable-fontconfig \
    --enable-fribidi \
    --enable-harfbuzz \
    --disable-pixman \
    --enable-buffer=static \
    --disable-gl-sdl \
    --enable-fb \
    --disable-directfb \
    --disable-wayland-shm \
    --disable-wayland-egl \
    --disable-sdl-primitive \
    --disable-software-xcb \
    --enable-software-xlib=static \
    --disable-gl-xcb \
    --enable-gl-xlib \
    --disable-image-loader-edb \
    --enable-image-loader-gif=static \
    --enable-image-loader-tiff=static \
    --enable-image-loader-svg=static \
    --enable-image-loader-eet=static \
    --enable-image-loader-generic=static \
    --disable-install-examples \
    --disable-tests \
    --disable-coverage \
    --disable-doc
  make
}

package() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  # install license files
  install -Dm644 "$srcdir/$pkgbase-$pkgver/COPYING" \
        "$pkgdir/usr/share/licenses/$pkgbase/COPYING"
}


