# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=flac
arch=('i686' 'x86_64')
url="http://flac.sourceforge.net/"
license=('BSD' 'GPL')
depends=('libogg')
makedepends=('nasm' 'xmms' 'doxygen')
options=('!libtool' '!makeflags')
source=(http://downloads.xiph.org/releases/flac/${pkgbase}-${pkgver}.tar.xz)

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --disable-sse
  make
}

check() {
  cd ${pkgbase}-${pkgver}
  make check
}

package_flac() {
  pkgdesc="Free Lossless Audio Codec"
  optdepends=('xmms: for using the xmms plugin')
  options=('!libtool' '!docs')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}

package_flac-doc() {
  pkgdesc="Developer documentation for the Free Lossless Audio Codec"
  depends=('flac')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" -C doc install
  sed -i "s|$srcdir/${pkgbase}-${pkgver}|/usr|" "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}/FLAC.tag"
  install -D -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}
