# Maintainer: Anh K. Huynh <kyanh@theslinux.org>

# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Original Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgver="${PACKAGE_VERSION##*.}"
pkgdesc="X Terminal Emulator"
arch=('i686' 'x86_64')
url="http://invisible-island.net/xterm/"
license=('custom')
depends=('libxft' 'libxaw' 'ncurses' 'xorg-luit' 'xbitmaps' 'libutempter')
source=(ftp://invisible-island.net/${PACKAGE_BASE}/${PACKAGE_BASE}-${_pkgver}.tgz
        LICENSE)

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --prefix=/usr \
      --libdir=/etc \
      --mandir=/usr/share/man \
      --with-app-defaults=/usr/share/X11/app-defaults/ \
      --with-x \
      --disable-full-tgetent \
      --disable-imake \
      --enable-ansi-color \
      --enable-88-color \
      --enable-256-color \
      --enable-broken-osc \
      --enable-broken-st \
      --enable-load-vt-fonts \
      --enable-i18n \
      --enable-wide-chars \
      --enable-doublechars \
      --enable-warnings \
      --enable-tcap-query \
      --disable-logging \
      --enable-dabbrev \
      --enable-freetype \
      --enable-luit \
      --enable-mini-luit \
      --enable-narrowproto \
      --enable-exec-xterm \
      --with-tty-group=tty \
      --with-utempter
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
  chmod 0755 "${pkgdir}/usr/bin/xterm"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" \
      "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m755 -d ${pkgdir}/usr/share/applications
  install -m644 ${srcdir}/${pkgname}-${_pkgver}/{xterm,uxterm}.desktop ${pkgdir}/usr/share/applications/
}
