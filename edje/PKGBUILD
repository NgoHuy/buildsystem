# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Ronald van Haren <ronald@archlinux.org>
# Original Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgdesc="A graphical design and layout library based on Evas"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('eina' 'ecore' 'evas' 'eio' 'lua' 'shared-mime-info' 'embryo')
optdepends=('python2: inkscape2edc')
install=edje.install
options=('!libtool' '!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgbase-$pkgver.tar.gz)

build() {
  cd $srcdir/$pkgbase-$pkgver

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' utils/inkscape2edc

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-sndfile \
    --disable-remix \
    --disable-vorbisenc \
    --disable-alsa \
    --disable-flac \
    --disable-tests \
    --disable-coverage \
    --enable-amalgamation \
    --disable-doc
  make
}

package(){
  cd $srcdir/$pkgbase-$pkgver

  make DESTDIR=$pkgdir install

  # install license files
  install -Dm644 $srcdir/$pkgbase-$pkgver/COPYING \
        $pkgdir/usr/share/licenses/$pkgbase/COPYING
}

