# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Dave Reisner <dreisner@archlinux.org>
# Original Contributor: Angel Velasquez <angvp@archlinux.org>
# Original Contributor: Eric Belanger <eric@archlinux.org>
# Original Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Original Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgdesc="An URL retrieval utility and library"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'libssh2' 'openssl' 'zlib')
options=('!libtool')
source=("http://curl.haxx.se/download/$pkgbase-$pkgver.tar.gz"{,.asc}
        0001-Fix-NULL-pointer-reference-when-closing-an-unused-mu.patch
        curlbuild.h)

ptrsize=$(cpp <<<'__SIZEOF_POINTER__' | sed '/^#/d')
case $ptrsize in
  8) _curlbuild=curlbuild-64.h ;;
  4) _curlbuild=curlbuild-32.h ;;
  *) error "unknown pointer size for architecture: %s bytes" "$ptrsize"
    exit 1
    ;;
esac

build() {
  cd "$pkgbase-$pkgver"

  patch -Np1 <"$srcdir/0001-Fix-NULL-pointer-reference-when-closing-an-unused-mu.patch"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-dependency-tracking \
      --disable-ldap \
      --disable-ldaps \
      --enable-ipv6 \
      --enable-manual \
      --enable-versioned-symbols \
      --enable-threaded-resolver \
      --without-libidn \
      --with-random=/dev/urandom \
      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt

  make
}

package() {
  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgbase/COPYING"

  # devel
  install -Dm644 docs/libcurl/libcurl.m4 "$pkgdir/usr/share/aclocal/libcurl.m4"
  mv "$pkgdir/usr/include/curl/curlbuild.h" "$pkgdir/usr/include/curl/$_curlbuild"
  install -m644 "$srcdir/curlbuild.h" "$pkgdir/usr/include/curl/curlbuild.h"
}
