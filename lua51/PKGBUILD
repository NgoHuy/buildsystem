# $Id $
# Original Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Original Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Original Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgdesc='A powerful light-weight programming language designed for extending applications'
arch=('i686' 'x86_64')
url='http://www.lua.org/'
depends=('readline')
license=('MIT')
options=('!makeflags' '!emptydirs')
source=("http://www.lua.org/ftp/lua-$pkgver.tar.gz"
        'lua-arch.patch'
        'lua-5.1-cflags.diff')

build() {
    cd lua-$pkgver
    patch -p1 -i "$srcdir/lua-arch.patch"
    patch -p1 -i "$srcdir/lua-5.1-cflags.diff"
    [[ $CARCH == x86_64 ]] && export CFLAGS="$CFLAGS -fPIC"
    sed -e 's:llua:llua5.1:' -e 's:/include:/include/lua5.1:' -i etc/lua.pc
    sed -r -e '/^LUA_(SO|A|T)=/ s/lua/lua5.1/' -e '/^LUAC_T=/ s/luac/luac5.1/' -i src/Makefile
    make MYCFLAGS="$CFLAGS" MYLDFLAGS="$LDFLAGS" linux
}

package() {
    cd lua-$pkgver
    make \
      TO_BIN="lua5.1 luac5.1" \
      TO_LIB="liblua5.1.a liblua5.1.so liblua5.1.so.5.1 liblua5.1.so.$pkgver" \
      INSTALL_DATA='cp -d' \
      INSTALL_TOP="$pkgdir/usr" \
      INSTALL_INC="$pkgdir/usr/include/lua5.1" \
      INSTALL_MAN="$pkgdir/usr/share/man/man1" \
      install
    install -D -m644 etc/lua.pc "$pkgdir/usr/lib/pkgconfig/lua5.1.pc"
    # Install the documentation
    install -d "$pkgdir/usr/share/doc/$pkgbase"
    install -m644 doc/*.{gif,png,css,html} "$pkgdir/usr/share/doc/$pkgbase"
    # Install copyrigth file
    install -D -m644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgbase/COPYRIGHT"
    # fixups
    ln -s liblua5.1.so "$pkgdir/usr/lib/liblua.so.5.1"
    ln -s liblua5.1.so "$pkgdir/usr/lib/liblua.so.$pkgver"
    cd "$pkgdir/usr/share/man/man1"
    mv lua.1 lua5.1.1
    mv luac.1 luac5.1.1
}

# vim:set ts=4 sw=4 et:
