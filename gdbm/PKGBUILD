# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Original Contributor: Allan McRae <allan@archlinux.org>
# Original Contributor: judd <jvinet@zeroflux.org>

pkgdesc="GNU database library"
url="http://www.gnu.org/software/gdbm/gdbm.html"
license=('GPL')
arch=('i686' 'x86_64')
depends=('glibc' 'sh')
source=(ftp://ftp.gnu.org/gnu/gdbm/${pkgbase}-${pkgver}.tar.gz
        gdbm-1.10-zeroheaders.patch)
options=('!libtool' '!makeflags')
install=gdbm.install
build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Prevent gdbm from storing uninitialized memory content
  # to database files. This patch improves security, as the
  # uninitialized memory might contain sensitive informations
  # from other applications.
  # https://bugzilla.redhat.com/show_bug.cgi?id=4457
  # http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=208927
  patch -Np1 -i ../gdbm-1.10-zeroheaders.patch

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --enable-libgdbm-compat

  make prefix=/usr
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make prefix="${pkgdir}"/usr \
       manprefix="${pkgdir}"/usr/share/man \
       man3dir="${pkgdir}"/usr/share/man/man3 \
       infodir="${pkgdir}"/usr/share/info \
       install
  
  # create symlinks for compatibility
  install -dm755 "${pkgdir}"/usr/include/gdbm
  ln -sf ../gdbm.h "${pkgdir}"/usr/include/gdbm/gdbm.h
  ln -sf ../ndbm.h "${pkgdir}"/usr/include/gdbm/ndbm.h
  ln -sf ../dbm.h  "${pkgdir}"/usr/include/gdbm/dbm.h
}
