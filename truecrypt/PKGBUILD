# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Paul Mattal <paul.archlinux.org>

epoch=1
pkgdesc="Free open-source cross-platform disk encryption software"
url="http://www.truecrypt.org/"
arch=('i686' 'x86_64')
license=('custom') # TrueCrypt License v2.6
depends=('fuse>=2.8.0' 'wxgtk>=2.8.9' 'libsm' 'device-mapper')
makedepends=('nasm')
optdepends=('sudo: mounting encrypted volumes as nonroot users')
conflicts=('truecrypt-utils')
replaces=('truecrypt-utils')
# N.B. Truecrypt's web-based source download is incompatible with
# makepkg. Source has been placed on ftp.archlinux.org instead
source=(ftp://ftp.archlinux.org/other/tc/truecrypt-${pkgver}.tar.gz{,.sig} \
        'ftp://ftp.archlinux.org/other/tc/pkcs-2.20.tar.gz' \
        "${pkgbase}.desktop" \
	"${pkgbase}-arch-detection.patch") 

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}-source"

  # Work around buggy CPU type detection
  patch -Np1 -i ../${pkgbase}-arch-detection.patch
  
  # Build
  make PKCS11_INC="${srcdir}/pkcs-2.20" LFLAGS+="-ldl"
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}-source"

  # Install Binary
  install -D -m755 Main/${pkgbase} "${pkgdir}/usr/bin/${pkgbase}"

  # Install Desktop file and Icon
  install -D -m644 "${srcdir}/${pkgbase}.desktop" "${pkgdir}/usr/share/applications/${pkgbase}.desktop"
  install -D -m644 Resources/Icons/TrueCrypt-48x48.xpm "${pkgdir}/usr/share/pixmaps/truecrypt.xpm"

  # Install License
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgbase}/License.txt"
}
