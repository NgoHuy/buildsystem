# Maintainer: Anh K. Huynh <kyanh@theslinux.org>
# Original Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgdesc="A library for reading vector images in Microsoft's native Windows Metafile Format (WMF)"
arch=('i686' 'x86_64')
url="http://wvware.sourceforge.net/libwmf.html"
license=('LGPL')
depends=('libpng' 'libx11' 'libjpeg' 'gsfonts' 'ghostscript')
makedepends=('gtk2' 'libxt')
optdepends=('gdk-pixbuf2: for pixbuf loader')
options=('!libtool' '!docs' '!emptydirs')
install=libwmf.install
source=( \
  http://downloads.sourceforge.net/sourceforge/wvware/${pkgbase}-${pkgver}.tar.gz
  libwmf-0.2.8.4-libpng-1.5.patch
  libwmf-0.2.8.4-useafterfree.patch)

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/libwmf-0.2.8.4-libpng-1.5.patch"
  patch -p1 -i "${srcdir}/libwmf-0.2.8.4-useafterfree.patch"
}

# FIXME: This package depends on Ghostscript!
build() {
  local _gs_version="$(gs -v | head -1 | awk '{print $3}')"
  local _gs_fontmap="/usr/share/ghostscript/${_gs_version}/Resource/Init/Fontmap.GS"
  ls "$_gs_fontmap" >/dev/null

  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-gsfontdir=/usr/share/fonts/Type1 \
    --with-fontdir=/usr/share/fonts/Type1 \
    --with-gsfontmap="$_gs_fontmap"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  #Remove fonts, these are in gsfonts
  rm -rf "${pkgdir}/usr/share/fonts"
  #Remove static GTK loader, can't use it anyways
  rm -f "${pkgdir}"/usr/lib/gtk-2.0/*/loaders/io-wmf.a
}
