# $Id: PKGBUILD 189652 2013-07-04 04:14:50Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=gd
pkgver=2.1.0
pkgrel=1
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('i686' 'x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('libpng' 'fontconfig' 'libjpeg')
optdepends=('perl: bdftogd script')
options=('!libtool')
source=(https://bitbucket.org/libgd/gd-libgd/downloads/libgd-${pkgver}.tar.xz)
md5sums=('03588159bf4faab9079849c8d709acc6')

build() {
  cd libgd-${pkgver}
  ./configure --prefix=/usr --without-xpm
  make
}

check() {
  cd libgd-${pkgver}
  make check
}

package() {
  cd libgd-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
