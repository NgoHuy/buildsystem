#$Id: PKGBUILD 157477 2012-04-28 19:41:37Z tomegun $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libcap
pkgver=2.22
pkgrel=3
pkgdesc="POSIX 1003.1e capabilities"
arch=('i686' 'x86_64')
url="http://sites.google.com/site/fullycapable/"
license=('GPL2')
depends=('glibc' 'attr')
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
md5sums=('b4896816b626bea445f0b3849bdd4077'
         '9d0983e25e5a251d098507f9561d2b27')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make prefix=/usr DESTDIR=${pkgdir} RAISE_SETFCAP=no install
  
  chmod 755 ${pkgdir}/usr/lib/libcap.so.${pkgver}
  rm ${pkgdir}/usr/lib/libcap.a
  
  install -Dm644 pam_cap/capability.conf \
    $pkgdir/usr/share/doc/$pkgname/capability.conf.example
}
