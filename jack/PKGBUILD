# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Ray Rashif <schiv@archlinux.org>
# Original Contributor: tobias <tobias@archlinux.net>
# Original Contributor: Robert Emil Berge <robert@rebi.no>

_longname=jack-audio-connection-kit
pkgdesc="A low-latency audio server"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('libsamplerate' 'readline')
makedepends=('doxygen' 'libffado' 'celt')
optdepends=('libffado: FireWire support'
            'celt: NetJACK driver')
url="http://jackaudio.org/"
backup=(etc/security/limits.d/99-audio.conf)
options=('!libtool')
provides=("$_longname=$pkgver")
conflicts=("$_longname")
replaces=("$_longname")
install=$pkgbase.install
source=("http://jackaudio.org/downloads/$_longname-$pkgver.tar.gz"
        '99-audio.conf'
        '40-hpet-permissions.rules'
        'ffado_setbuffsize-jack1.patch')

build() {
  cd "$srcdir/$_longname-$pkgver"

  # backport firewire stuff
  #   - needed for setbuffsize feature in latest stable ffado
  # from https://github.com/jackaudio/jack1/commit/025d3ad
  patch -Np1 -i "$srcdir/ffado_setbuffsize-jack1.patch"

  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "$srcdir/$_longname-$pkgver"

  make DESTDIR="$pkgdir" install

  # configure realtime access/scheduling
  # see https://bugs.archlinux.org/task/26343
  install -Dm644 "$srcdir/99-audio.conf" \
    "$pkgdir/etc/security/limits.d/99-audio.conf"

  install -Dm644 "$srcdir/40-hpet-permissions.rules" \
    "$pkgdir/usr/lib/udev/rules.d/40-hpet-permissions.rules"

  # install a missing header forgotten by upstream
  # see https://bugs.archlinux.org/task/26865
  install -Dm644 "$srcdir/$_longname-$pkgver/jack/jslist.h" \
    "$pkgdir/usr/include/jack/jslist.h"
}

# vim:set ts=2 sw=2 et:
