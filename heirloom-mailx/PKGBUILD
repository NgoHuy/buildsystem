# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Original Contributor: Sergej Pupykin <pupykin.s@arch@gmail.com>
# Original Contributor: Andreas Wagner <Andreas.Wagner@em.uni-frankfurt.de>

pkgdesc="A commandline utility for sending and receiving email"
arch=('i686' 'x86_64')
url="http://heirloom.sourceforge.net/mailx.html"
license=('custom')
groups=('base')
depends=('openssl' 'krb5')
optdepends=('smtp-forwarder: for sending mail')
replaces=('mailx' 'mailx-heirloom')
provides=('mailx' 'mailx-heirloom')
conflicts=('mailx' 'mailx-heirloom')
backup=(etc/mail.rc)
source=(ftp://ftp.archlinux.org/other/${pkgbase}/${pkgbase}-${pkgver}.tar.xz)
options=('!makeflags') # Does not build with MAKEFLAGS=-jX, X>1

# source PKGBUILD && mksource
mksource() {
  export CVSROOT=:pserver:anonymous@nail.cvs.sourceforge.net:/cvsroot/nail
  D=nail
  [ -d "${D}" ] && cvs up "${D}" || cvs co "${D}"
  
  _dirname=${pkgbase}-${pkgver}
  mv $D ${_dirname}
  tar -cJv --exclude=CVS -f ${_dirname}.tar.xz ${_dirname}
  rm -rf ${_dirname}
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  sed -i 's|/etc/nail.rc|/etc/mail.rc|g' mailx.1

  # For Linux and BSD, this should be set.
  echo "set bsdcompat" >> nail.rc

  sed -i "s/pg/less/" cmd1.c

  echo PREFIX=/usr \
       MAILRC=/etc/mail.rc \
       SENDMAIL=/usr/sbin/sendmail \
       MAILSPOOL=/var/spool/mail \
       UCBINSTALL=/usr/bin/install > makeflags

  make `cat makeflags` IPv6=-DHAVE_IPv6_FUNCS
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" `cat makeflags` install

  # For compatibility with the old mailx program
  ln -sf mailx "${pkgdir}"/usr/bin/mail
  ln -sf mailx.1.gz "${pkgdir}"/usr/share/man/man1/mail.1.gz

  install -D -m0644 COPYING "${pkgdir}"/usr/share/licenses/${pkgbase}/LICENSE
}
