# Maintainer: Anh K. Huynh <kyanh@theslinux.org>
# Original Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Original Contributor: Tobias Powalowski <tpowa@archlinux.org>
pkgdesc="Linux Key Management Utilities"
arch=(i686 x86_64)
url="http://www.kernel.org"
license=('GPL2' 'LGPL2.1')
depends=('glibc' 'sh')
backup=(etc/request-key.conf)
source=( \
  request-key.conf.patch \
  http://people.redhat.com/~dhowells/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2)

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  # Fix paths of binaries in /etc/request-key.conf.
  # The purpose is to replace `/bin/foobar` by `/usr/bin/foobar`.
  # I wonder when Redhat guys can support this in their own source
  patch -Np0 -i ../request-key.conf.patch
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" LIBDIR="/usr/lib" USRLIBDIR="/usr/lib"  install
  # add nfsidmap to config file #30396
  echo "# NFS idmap resolver" >> $pkgdir/etc/request-key.conf
  echo "create id_resolver * * /usr/sbin/nfsidmap %k %d" \
       >> $pkgdir/etc/request-key.conf
}
