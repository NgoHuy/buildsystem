# $Id: PKGBUILD 191770 2013-07-30 11:05:39Z jgc $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Arjan Timmerman <arjan.archlinux.org>
# Contributor: Tom Newsom <Jeepster.gmx.co.uk>

pkgname=imlib2
pkgver=1.4.5
pkgrel=5
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng' 'libid3tag' 'libjpeg-turbo')
options=('!libtool')
source=(http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2
        imlib2-giflib5.patch)
sha1sums=('af86a2c38f4bc3806db57e64e74dc9814ad474a0'
          '29d0778ffbe6e57f08048918a79eb1ad6af87bce')

prepare() {
  cd $pkgname-$pkgver
  patch -Np0 -i ../imlib2-giflib5.patch
}

build() {
  cd $pkgname-$pkgver

  [ $CARCH = "i686" ] && EXTRAOPTS="--enable-mmx"
  [ $CARCH = "x86_64" ] && EXTRAOPTS="--enable-amd64"

  ./configure --prefix=/usr \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib $EXTRAOPTS
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Install License
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
