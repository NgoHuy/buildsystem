# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Original Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Original Contributor: Darwin Bautista <djclue917@gmail.com>

pkgdesc="Shared libraries for GSM 06.10 lossy speech compression"
arch=('i686' 'x86_64')
url="http://www.quut.com/gsm/"
license=('custom')
depends=('glibc')
source=("http://www.quut.com/${pkgbase}/${pkgbase}-${pkgver}.tar.gz"
        'gsm.patch')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}-pl${pkgver##*.}/"

  patch -Np0 -i "${srcdir}/${pkgbase}.patch"

  # flags for shared lib
  CFLAGS="${CFLAGS} -fPIC"
  make CCFLAGS="-c ${CFLAGS}"
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}-pl${pkgver##*.}/"

  # Prepare directories
  install -m755 -d "${pkgdir}"/usr/{bin,lib,include/gsm,share/{licenses/${pkgbase},man/man{1,3}}}

  make -j1 INSTALL_ROOT="${pkgdir}/usr" \
    GSM_INSTALL_INC="${pkgdir}/usr/include/gsm" \
    GSM_INSTALL_MAN="${pkgdir}/usr/share/man/man3" \
    TOAST_INSTALL_MAN="${pkgdir}/usr/share/man/man1" \
    install

  # Install license
  install -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgbase}/license.txt
}
