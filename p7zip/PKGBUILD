# Maintainer: Severus <severus@theslinux.org>
# Original Contributor: Thayer Williams <thayer@archlinux.org>
# Original Contributor: Hugo Doria <hugo@archlinux.org>
# Original Contributor: TuxSpirit<tuxspirit@archlinux.fr>  2007/11/17 21:22:36 UTC
# Original Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Original Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgdesc='Command-line version of the 7zip compressed file archiver'
url='http://p7zip.sourceforge.net/'
license=('GPL' 'custom')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'bash')
optdepends=('wxgtk: GUI'
            'desktop-file-utils: desktop entries')
makedepends=('yasm' 'nasm' 'wxgtk')
source=("http://downloads.sourceforge.net/project/${pkgbase}/${pkgbase}/${pkgver}/${pkgbase}_${pkgver}_src_all.tar.bz2"
        '7zFM.desktop')

options=('!makeflags')
install=install

build() {
	cd "${srcdir}/${pkgbase}_${pkgver}"

	[[ $CARCH = x86_64 ]] \
	&& cp makefile.linux_amd64_asm makefile.machine \
	|| cp makefile.linux_x86_asm_gcc_4.X makefile.machine

	make all4 OPTFLAGS="${CXXFLAGS}"
}

package() {
	cd "${srcdir}/${pkgbase}_${pkgver}"

	make install \
		DEST_DIR="${pkgdir}" \
		DEST_HOME="/usr" \
		DEST_MAN="/usr/share/man"

	# Licenses
	install -d "${pkgdir}"/usr/share/licenses/p7zip
	ln -s -t "${pkgdir}"/usr/share/licenses/p7zip \
		/usr/share/doc/p7zip/DOCS/License.txt \
		/usr/share/doc/p7zip/DOCS/unRarLicense.txt

	# Integration with stuff...
	install -Dm644 GUI/p7zip_32.png "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/p7zip.png
	install -d "${pkgdir}"/usr/share/{applications,kde4/services/ServiceMenus}
	cp GUI/kde4/* "${pkgdir}"/usr/share/kde4/services/ServiceMenus/
	cp ../7zFM.desktop "${pkgdir}"/usr/share/applications/
	ln -s 7zCon.sfx "${pkgdir}"/usr/lib/p7zip/7z.sfx

	find GUI/help -type d -exec chmod 755 {} \;
	cp -r GUI/help "${pkgdir}"/usr/lib/p7zip/

	chmod -R u+w "${pkgdir}/usr"
}
