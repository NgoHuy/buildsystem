# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Original Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgdesc="Realtime Policy and Watchdog Daemon"
arch=(i686 x86_64)
url="http://git.0pointer.de/?p=rtkit.git"
license=(GPL 'custom:BSD')
depends=(dbus polkit systemd)
install=rtkit.install
source=(http://0pointer.de/public/$pkgbase-$pkgver.tar.xz
        libsystemd.patch systemd205.patch)

prepare() {
  cd $pkgbase-$pkgver
  patch -Np1 -i ../libsystemd.patch
  patch -Np1 -i ../systemd205.patch
  autoreconf -fi
}

build() {
  cd $pkgbase-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/$pkgbase \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make

  ./rtkit-daemon --introspect > org.freedesktop.RealtimeKit1.xml
}

package() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 org.freedesktop.RealtimeKit1.xml \
    "$pkgdir/usr/share/dbus-1/interfaces/org.freedesktop.RealtimeKit1.xml"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgbase/LICENSE"
  sed -ne '4,25p' rtkit.c >"$pkgdir/usr/share/licenses/$pkgbase/COPYING"
}
