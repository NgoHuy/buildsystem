# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>

pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('valgrind' 'clang')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/$pkgbase/$pkgbase-$pkgver.tar.bz2
        COPYING)

build() {
  cd $pkgbase-$pkgver

  # pthread is useless in Linux
  sed -i "/pthread-stubs/d" configure.ac
  autoreconf --force --install
  
  # gcc 4.8 introduces graphic corruption and X crashes with nouveau cards (nv44)
  # http://gcc.gnu.org/ml/gcc-help/2013-07/msg00103.html
  # we use clang compiler meanwhile
  export CC=clang

  ./configure --prefix=/usr --enable-udev
  make
}

check() {
  cd $pkgbase-$pkgver
  make -k check
}

package() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgbase"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgbase/"
}
