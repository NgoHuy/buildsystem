# $Id: PKGBUILD 185033 2013-05-10 14:15:05Z bpiotrowski $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=libvpx
pkgver=1.2.0
pkgrel=1
pkgdesc="The VP8 Codec SDK"
arch=('i686' 'x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=(ftp://ftp.archlinux.org/other/libvpx/libvpx-$pkgver.tar.xz{,.sig})
sha1sums=('75cd953cf2db694412569272803fbdc73f2b599f'
          'SKIP')

build() {
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr --enable-vp8 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DIST_DIR="$pkgdir/usr" install
    install -D -m 0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
