# $Id: PKGBUILD 166858 2012-09-20 03:29:26Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=m4
pkgver=1.4.16
pkgrel=2
pkgdesc="The GNU macro processor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/m4"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'bash')
install=m4.install
source=(ftp://ftp.gnu.org/gnu/m4/$pkgname-$pkgver.tar.xz{,.sig}
        m4-1.4.16-readlink-einval.patch)
md5sums=('7548ec061a1ba993790159764f522d0e'
         'eebe5c94e74e7551e2e30c5844f1b653'
         'cea138fa9b568d06e46269611cec8dd0')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  
  # glibc-2.16 compatibility
  sed -i -e '/gets is a/d' lib/stdio.in.h
  
  # patch testsuite failure with newer kernels
  patch -Np1 -i $srcdir/m4-1.4.16-readlink-einval.patch
  
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/$pkgname-$pkgver
  make check
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make prefix=${pkgdir}/usr install
}
