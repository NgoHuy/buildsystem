# $Id: PKGBUILD 194724 2013-09-23 09:49:14Z jgc $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>

pkgname=gsettings-desktop-schemas
pkgver=3.10.0
pkgrel=1
pkgdesc="Shared GSettings schemas for the desktop"
arch=(any)
url="http://live.gnome.org/"
license=(GPL)
depends=(glib2)
makedepends=(intltool gobject-introspection)
install=$pkgname.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('ca681596b0fd35a061c50bf99a5f5bdcf245ccc60019895edd2aef9f79c7a8ce')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
