# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Original Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Original Contributor: Martin Sandsmark <martin.sandsmark@kde.org>

pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('LGPL2.1' 'GPL2')
depends=('a52dec' 'libdvbpsi' 'libxpm' 'libdca' 'qt4' 'libproxy' 
         'sdl_image' 'libdvdnav' 'libtiger' 'lua51' 'libmatroska' 
         'zvbi' 'taglib' 'libmpcdec' 'ffmpeg-compat' 'faad2' 'libupnp' 
         'libshout' 'libmad' 'libmpeg2' 'xcb-util-keysyms')
makedepends=('live-media' 'libnotify' 'libbluray' 'flac' 'kdelibs'
             'libdc1394' 'libavc1394' 'lirc-utils' 'libcaca' 'oss' 
             'librsvg' 'portaudio' 'oss' 'libgme' 'xosd' 'projectm' 
             'twolame' 'aalib' 'libmtp' 'libdvdcss' 'gnome-vfs' 
             'libgoom2' 'libtar' 'vcdimager' 'opus' 'libssh2' 'mesa')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libnotify: for notification plugin'
            'ncurses: for ncurses interface support'
            'libdvdcss: for decoding encrypted DVDs'
            'lirc-utils: for lirc plugin'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'kdelibs: KDE Solid hardware integration'
            'libva-vdpau-driver: vdpau back-end for nvidia'
            'libva-intel-driver: back-end for intel cards'
            'libbluray: for Blu-Ray support'
            'flac: for Free Lossless Audio Codec plugin'
            'oss: for OSS audio support'
            'portaudio: for portaudio support'
            'twolame: for TwoLAME mpeg2 encoder plugin'
            'projectm: for ProjectM visualisation plugin'
            'libcaca: for colored ASCII art video output'
            'libgme: for libgme plugin'
            'librsvg: for SVG plugin'
            'gnome-vfs: for GNOME Virtual File System support'
            'libgoom2: for libgoom plugin'
            'vcdimager: navigate VCD with libvcdinfo'
            'xosd: for xosd support'
            'aalib: for ASCII art plugin'
            'libmtp: for MTP devices support'
            'smbclient: for SMB access plugin'
            'libcdio: for audio CD playback support'
            'ttf-freefont: for subtitle font '
            'ttf-dejavu: for subtitle font'
            'opus: for opus support'
            'libssh2: for sftp support')
conflicts=('vlc-plugin')
replaces=('vlc-plugin')
backup=('usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!libtool' '!emptydirs')
install=vlc.install
source=("http://download.videolan.org/pub/videolan/${pkgbase}/${pkgver/.a}/${pkgbase}-${pkgver/.a/a}.tar.xz"
        vlc-2.0.7-vaapi-compat.patch
        vlc-2.0.8-Fix-live555-breakage-in-last-update.patch)

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver/.a}"

  patch -Np1 -i ../vlc-2.0.7-vaapi-compat.patch
  patch -Np1 -i ../vlc-2.0.8-Fix-live555-breakage-in-last-update.patch

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c
  sed -i -e 's:truetype/ttf-dejavu:TTF:g' modules/visualization/projectm.cpp
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver/.a}"

  CFLAGS+="-I/usr/include/samba-4.0" CPPFLAGS+="-I/usr/include/samba-4.0" \
  PKG_CONFIG_PATH+="/usr/lib/ffmpeg-compat/pkgconfig" \
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-rpath \
              --enable-oss \
              --enable-faad \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --enable-realrtsp \
              --enable-xosd \
              --enable-aa \
              --enable-vcdx \
              --enable-upnp \
              --enable-opus \
              --enable-sftp \
              LUAC=luac5.1 \
              RCC=/usr/bin/rcc-qt4

  make
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver/.a}"

  make DESTDIR="${pkgdir}" install

  for res in 16 32 48 128; do
    install -D -m644 "${srcdir}/vlc-${pkgver/.a}/share/icons/${res}x${res}/vlc.png" \
        "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done
}
