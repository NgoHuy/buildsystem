# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgdesc="GTK+ Web content engine library for GTK+ 2.0"
arch=(i686 x86_64)
url="http://webkitgtk.org/"
license=(custom)
depends=(libxt libxslt sqlite libsoup enchant libgl geoclue gtk2 gstreamer0.10-base
         icu)
makedepends=(gperf gobject-introspection python2 mesa ruby gtk-doc)
optdepends=()
provides=("libwebkit=${pkgver}")
conflicts=(libwebkit)
replaces=(libwebkit)
options=(!libtool !emptydirs)
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz)

build() {
  cd webkitgtk-$pkgver

  # Suppress a *lot* of gcc 4.8 warnings
  CPPFLAGS+=" -Wno-unused-local-typedefs"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --libexecdir=/usr/lib/$pkgbase \
    --enable-introspection \
    --disable-webkit2 \
    --with-gstreamer=0.10 \
    --with-gtk=2.0

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make all stamp-po
}

package() {
  cd webkitgtk-$pkgver
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/$pkgbase/LICENSE"
  rm -r "$pkgdir/usr/share/gtk-doc/html/webkitgtk"
}
