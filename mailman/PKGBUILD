# Maintainer: Severus <severus@theslinux.org>
# Original Maintainer: SÃ©bastien Luttringer
# Original Contributor: Paul Mattal <paul@archlinux.org>

pkgdesc='The GNU Mailing List Manager'
arch=(i686 x86_64)
license=('GPL')
url='http://www.list.org/'
depends=('glibc' 'python2' 'smtp-server')
# we need to have the mailman user to build mailman
makedepends=('mailman')
# 'Defaults.py' should not be changed by users; 'mm_cfg.py' should instead.
backup=('usr/lib/mailman/Mailman/mm_cfg.py')
install=$pkgbase.install
source=("http://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tgz"
        'mailman-2.1-build.patch'
        "$pkgbase.profile.sh"
        "$pkgbase.profile.csh"
        "$pkgbase.service")

build() {
  cd $pkgbase-$pkgver
  # some files in mailman doesn't use configure parameter
  find . -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
  # fix directory permissions to satisfy check_perms
  patch -p1 -i "$srcdir/mailman-2.1-build.patch"
  # set some sane defaults before the configure script has a chance to screw them up
  ./configure \
    --without-permcheck \
    --prefix=/usr/lib/mailman \
    --with-var-prefix=/var/lib/mailman \
    --with-mail-gid=mailman \
    --with-username=mailman \
    --with-groupname=mailman \
    --with-cgi-gid=http \
    --with-python=/usr/bin/python2 \
    --with-mailhost=localhost \
    --with-urlhost=localhost
  make
}

package() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  # let's follow Fedora FHS way; Gentoo does it the other way round

  # Create a link so that the config file mm_cfg.py appears in config
  # directory /etc/mailman. We don't put mm_cfg.py in the config directory
  # because its executable code (python file) and the security policy wants
  # to keep executable code out of /etc and inside of a lib directory instead,
  # and because traditionally mm_cfg.py was in the Mailman subdirectory and
  # experienced mailman admins will expect to find it there. But having it
  # "appear" in the config directory is good practice and heading in the
  # right direction for FHS compliance.
  install -d -m755 "$pkgdir/etc/$pkgbase"
  ln -sv /usr/lib/mailman/Mailman/mm_cfg.py "$pkgdir/etc/$pkgbase/mm_cfg.py"
  # fix some permissions to satisfy check_perms
  chown -R mailman:mailman "$pkgdir"/{usr/lib/mailman,var/lib/mailman,etc/mailman/*}
  chown http:mailman "$pkgdir"/var/lib/mailman/archives/private
  chmod 2770 "$pkgdir"/var/lib/mailman/archives/private
  chmod 2755 "$pkgdir"/usr/lib/mailman/cgi-bin/*
  chmod 2755 "$pkgdir"/usr/lib/mailman/mail/mailman
  cd "$srcdir"
  # systemd
  install -Dm 644 $pkgbase.service \
    "$pkgdir/usr/lib/systemd/system/$pkgbase.service"
  # profiles
  install -Dm 755 $pkgbase.profile.sh "$pkgdir/etc/profile.d/$pkgbase.sh"
  install -Dm 755 $pkgbase.profile.csh "$pkgdir/etc/profile.d/$pkgbase.csh"
}

# vim:set ts=2 sw=2 et:
